# Enable Powerlevel10k instant prompt
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Ensure path arrays do not contain duplicates
typeset -gU cdpath fpath mailpath path

path=(
  .
  $HOME/bin
  $HOME/{.local,code/go}/bin
  $HOME/.local/share/{chezmoi,chezmoi-private}/bin
  /opt/homebrew/{bin,sbin}
  /usr/sbin
  $path
)

fpath=(
  ~/.zsh_completions
  $fpath
)

# Initialize zcomet
if [[ ! -f ${ZDOTDIR:-${HOME}}/.zcomet/bin/zcomet.zsh ]]; then
  command git clone https://github.com/agkozak/zcomet.git ${ZDOTDIR:-${HOME}}/.zcomet/bin
fi
source ${ZDOTDIR:-${HOME}}/.zcomet/bin/zcomet.zsh

# Setup for Bash completion support
autoload bashcompinit && bashcompinit

# Setup completions directory
[[ ! -d ~/.zsh_completions ]] && mkdir -p ~/.zsh_completions
zcomet fpath ~/.zsh_completions

# Prompt theme
zcomet load romkatv/powerlevel10k

zcomet load agkozak/zsh-z
zcomet load mollifier/cd-gitroot
zcomet load reegnz/jq-zsh-plugin

# Load Ibotta shell tools
#zcomet load ibotta/shell-tools@ver/new-structure

# History substring search
zcomet load zsh-users/zsh-history-substring-search
bindkey -M vicmd "k" history-substring-search-up
bindkey -M vicmd "j" history-substring-search-down

# Completion configuration
zstyle ':completion:*' use-cache yes
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:::::' completer _complete _approximate
zstyle ':completion:*' menu no

# Load fzf bindings and completion
zcomet load junegunn/fzf@v0.65.2 /shell/{key-bindings,completion}.zsh

# Load fzf-tab and configure
zcomet load Aloxaf/fzf-tab
zstyle ":fzf-tab:*" fzf-flags --border
zstyle ':fzf-tab:*' fzf-min-height 15
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -F -g -h --group-directories-first --git --icons --color-scale --color=always --hyperlink -1 $realpath'
zstyle ':fzf-tab:*' fzf-bindings 'space:accept'

# Load local configurations
for f in ~/.zshrc.d/*.plugin.zsh; do zcomet snippet "${f}"; done
for f in ~/.zshrc-private.d/*.plugin.zsh; do zcomet snippet "${f}"; done

# Load autosuggestions and syntax highlighting
zcomet load zsh-users/zsh-autosuggestions
bindkey '^ ' autosuggest-accept

# catppuccin theme for syntax highlighting (uncomment to enable)
zcomet load elementalvoid/zsh-fast-syntax-highlighting
# fast-theme catppuccin 2>&1 >/dev/null

# Load and cache completions
zcomet compinit

# Load p10k configuration
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
(( ! ${+functions[p10k]} )) || p10k finalize
